model{


# response 0 = negative, response 1 = positive for infection
# state 1 = not infected, state 2 = infected


## A. state conditional responses

for (t in 1:T){
    for(i in 1:N){
      for(k in 1:K){
        tests[t,i,k]~dbern(crp[k,curState[t,i]])
      }
    }
}

# in the model without random subject effects, the item response probas are just probability parameters to be estimated, so just put a non-informative prior on them
# the probability vectors below give the probabilities of a positive response given the latent infection state (first element = p(pos | uninfected), second element = p(pos | infected))
for(state in 1:2){
  for(k in 1:K){
    crp[k,state] ~ dbeta(0.5,0.5) # Jeffrey's prior for a probability parameter
  }
}


## B. transition probabilities (time homogeneous transition matrix model) - note we treat these as parameters to be estimated, so we just use them to derive the current state, then set non-informative priors on them

for(i in 1:N){
      curState[1,i] ~ dcat(c(1-pInfState[1,i],pInfState[1,i]))
      pInfState[1,i]<-pInfInit

      for(t in 2:T){
      	curState[t,i] ~ dcat(c(1-pInfState[t,i],pInfState[t,i]))
	pInfState[t,i]<-ifelse(curState[t-1,i]==1,pUninf2Inf,pInf2Inf)
      }
}

#pInf2Inf ~ dunif(0,0.5) # uninformative, but forces this to be less than 0.5 - acknowledges that infection is more unlikely than likely; could have been a solution to the label switching
pInf2Inf ~ dbeta(0.5,0.5) # Jeffrey's prior for a probability parameter
#pUninf2Inf ~ dunif(0,0.5) # uninformative, but forces this to be less than 0.5 - acknowledges that infection is more unlikely than likely; could have been a solution to the label switching
pUninf2Inf ~ dbeta(0.5,0.5) # Jeffrey's prior for a probability parameter

## C. initial infection state probability - a parameter to be estimated, so just slap a non-informative prior on it    
pInfInit<-pUninf2Inf/(1+pUninf2Inf-pInf2Inf) # stationary solution; required for model identifiability
#pInfInit ~ dbeta(0.5,0.5) # Jeffrey's prior for a probability parameter
#pInfInit ~ dunif(0,0.5) # uninformative, but forces this to be less than 0.5 - acknowledges that infection is more unlikely than likely

}
